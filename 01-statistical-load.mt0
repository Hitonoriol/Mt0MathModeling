/*
* Statistical load calculation of vibration isolation (damping) system
* 	with four dampers located in the horizontal plane (xOz).
*/

_ = nil
/* Variant #6 */
x = $(_, 18.0, 16.0, -1.0, -2.0)
z = $(_, 21.0, -2.0, -6.0, 11.0)

setw = func: str, width {
	if (str.size() >= width) ?
		return: str

	pad = " ".repeat(width - str.size())
	return: str :: pad
}

print_table = func: x, z {
	w = 12
	<% "".setw(3) :: "X".setw(w) :: "Y".setw(w) :: "Z".setw(w)
	range(x.size() - 1).for_each(@: i -> <% setw($$(i + 1), 3) :: setw($$x[i + 1], w) :: setw($$0, w) :: setw($$z[i + 1], w))
}

mul = func: list -> $(res = 1, list.for_each(@: x -> res *= x))[0]
sub = func: list -> list[0] - list[1]
sum = func: list -> $(res = 0, list.for_each(@: x -> res += x))[0]
calc_d = func: zzx, xx -> $(
				res = 0
				range(3).for_each(@: i -> res += mul(zzx[i]) * sub(xx[i]))
			  )[0]

d = $(
	calc_d($($(z[2], z[4], x[3]), $(z[2], z[3], x[4]), $(z[3], z[4], x[2]))
		$($(x[4], x[2]), $(x[2], x[3]), $(x[3], x[4]))
	)
	calc_d($($(z[1], z[4], x[3]), $(z[1], z[3], x[4]), $(z[3], z[4], x[1]))
		$($(x[4], x[1]), $(x[1], x[3]), $(x[3], x[4]))
	)
	calc_d($($(z[1], z[4], x[2]), $(z[1], z[2], x[4]), $(z[2], z[4], x[1]))
		$($(x[4], x[1]), $(x[1], x[2]), $(x[2], x[4]))
	)
	calc_d($($(z[1], z[3], x[2]), $(z[1], z[2], x[3]), $(z[2], z[3], x[1]))
		$($(x[3], x[1]), $(x[1], x[2]), $(x[2], x[3]))
	)
)
D = sum(d)

print_table(x, z)

<% newl :: "Determinants:"
range(4).for_each(@: i -> %% "D" :: (i + 1) :: " = " :: (d[i]) :: (i != 3 ? ", " : newl))
<% "D = " :: D

<% newl :: "Loads:"
range(4).for_each(@: i -> <% "P" :: (i + 1) :: " = " :: (d[i] / D) :: "G")