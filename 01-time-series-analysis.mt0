/* Вариант #6 */
_ = nil

/* Входной временной ряд */
y =   $(_
	2.3
	2.8
	3.5
	3.0
	2.6
	3.3
	4.0
	3.5
	3.0
	3.7
	4.4
	4.2
	3.9
	3.2
	4.1
	5.0
	4.5
	4.4
	4.7
	5.2
	4.4
	3.5
	4.1
	4.6
	4.2
	3.4
	15.0
	12.7
	1.3
	1.3
       )

/*
 * Табличное значение критерия Ирвина для заданного количества степеней ряда
 * 			(в этом случае n = 30)
 */
IRV_A = 1.2

setw = func: str, width {
	if (str.size() >= width) ?
		return: str

	pad = " ".repeat(width - str.size())
	return: str :: pad
}

center = func: str, width {
	pad = " ".repeat((width - str.size()) / 2)
	return: pad :: str :: pad
}

sum = func: list -> $(res = 0, range(1, list.size()).for_each(@: i -> res += list[i]))[0]

/* Расчет среднего уровня ряда: (1 / n) * Σ(yt) */
mean = func: y {
	result = sum(y) / #N
	<% newl :: "Расчет среднего уровня ряда" :: newl
		:: "y‾ = (1 / " :: #N :: ") * Σ" :: y.map(@: n -> n != nil ? n) :: " = " :: result :: newl
	return: result
}

/* Расчет среднеквадратичного отклонения: √(Σ((yt - y‾)^2) / n) */
qmean = func: y {
	sq = y.map(#(yt) {
		if (yt != nil) ?
			return: pow(yt - #MEAN, 2)
	})
	result = sqrt(sum(sq) / #N)
	<% newl :: "Расчет среднеквадратичного отклонения" :: newl
		:: "σy = √(Σ" :: sq :: " / " :: #N :: ") = " :: result :: newl
	return: result
}

/* Расчет вектора λ критерия Ирвина */
calc_lambda = func: y, qmean {
	<% newl :: "Расчет λ-статистик критерия Ирвина"
	return: range(2, #N + 1).map(#(t, y => y, qmean => qmean) {
		l = abs(y[t] - y[t - 1]) / qmean
		%% "λ" :: t :: " = |" :: y[t] :: " - " :: y[t - 1] :: "| / " :: qmean :: " = " :: l
			:: (t != #N ? ";    " : (newl :: newl))
		%% ((t + 1) % 2 == 0 ? newl : "")
		return: l
	})
}

/*
 * Проверка на аномальность по критерию Ирвина
 */
 E = 0.000001
check_lambda = func: lambda {
	%% newl :: "Проверка на аномальность" :: newl
	has_anomalies = false
	l_delta = nil
	range(lambda.size()).for_each(#(t) {
		if (!has_anomalies) ? {
			has_anomalies = (lambda[t] - #E) >= #IRV_A
			%% "λ" :: (t + 2) :: " = " :: lambda[t] :: (has_anomalies ? " >= " : " < ") :: "λα = " :: #IRV_A
				:: (t != lambda.size() ? ";    " : (newl :: newl))
			%% ((t + 1) % 2 == 0 ? newl : "")
			if (has_anomalies) ? {#ANOMAL_T = t + 2}
		}
	})
	return: has_anomalies
}

/*
 * Исправление аномального уровня ряда: y[t] = λα * σy + y[t-1],
 * 	где для σy и y[t-1] берутся их значения с прошлой итерации.
 */
fix_anomaly = func: y, t {
	y[t] = #IRV_A * #QMEAN + #y[t - 1]
	<% newl :: $"Исправление аномалии: y[{1}]({2}) = λα * σy({4}) + y[{3}]({4})" t, #IT, t-1, #IT - 1
	<% $"λα = {2}; σy({3}) = {4}; y[{1}]({3}) = {5}" t - 1, #IRV_A, #IT - 1, #QMEAN, #y[t - 1]
	<% "* y"
		:: t :: " = " :: #IRV_A :: " * " :: #QMEAN :: " + " :: #y[t - 1] :: " = " :: y[t] :: newl
	return: y[t]
}

TW = 4; W = 9; MAXW = 2 * (TW + W) + W
print_series = func: y {
	global: W, TW
	<% ("t".setw(TW) :: "Yt".setw(W) :: "".setw(W)).repeat(2)
	range(1, 16).for_each(@: i -> <%	($$i).setw(TW) :: ($$y[i]).setw(W)
						:: "".setw(W)
						:: ($$(i + 15)).setw(TW) :: ($$y[i + 15]).setw(W))
}

/* Вывод характеристик ряда */
print_characteristics = func @() {
	<% (#IT != 0 ? ("Итерация #" :: #IT) : "Исходный временной ряд").center(#MAXW)
	#y.print_series()
	<% newl :: "Характеристики ряда"
	<% "* y‾ = " :: (#MEAN = mean(#y))
	<% "* σy = " :: (#QMEAN = qmean(#y))
	<% "* λ = " :: (#LAMBDA = calc_lambda(#y, #QMEAN))
}

/* Уровни ряда */
N = y.size() - 1

/* Средний уровень ряда и и среднее квадратичное отклонение */
MEAN = 0; QMEAN = 0

/* λ вектор для метода Ирвина */
LAMBDA = $()

/* Номер итерации устранения аномалий */
IT = 0

/* Текущий уровень t, являющийся аномальным */
ANOMAL_T = 0

print_characteristics()

delim = "-".repeat(90)

while(check_lambda(LAMBDA)) {
	++IT
	fix_anomaly(y, ANOMAL_T)
	<% delim :: newl
	print_characteristics()
}

<% newl :: newl :: "Все аномалии устранены."